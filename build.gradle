apply from: 'https://raw.githubusercontent.com/concordion/concordion-extension-build/master/extension-build.gradle'

//TODO: Remove me once new concordion snapshot published
repositories {
    mavenLocal()
}
    
description = 'An extension to Concordion that capture screenshots, etc in storyboard format while the specification is being executed so that we can see exactly what the test has done against the system under test'

ext {
	developers = {
		developer {
			id 'andrew-sumner'
			name 'Andrew Sumner'
			roles { role 'Project owner' }
		}
	}
	inceptionYear = '2014'
}

githubPages {
	repoUri = 'git@github.com:concordion/concordion-storyboard-extension.git'
	pages {
		from 'docs'

		from ('build/reports/spec/') {
			into 'spec'
		}
			
		from ('build/docs/javadoc/') {
			into 'api'
		}
	}
}

dependencies {
	testCompile "org.concordion:concordion-embed-extension:1.1.2"

	//TODO: Remove me once new concordion snapshot published
	compile 'org.concordion:concordion:2.0.0-SNAPSHOT'
	testCompile 'org.concordion:concordion:2.0.0-SNAPSHOT:tests'
}

sourceSets {
	test {
		resources { srcDirs += 'src/test/java' }
	}
}

test.dependsOn cleanTest
test {
	include '**/Storyboard.*'
	systemProperties['concordion.output.dir'] = "$reporting.baseDir/spec"
	outputs.upToDateWhen { false } // force it to run even if test code hasn't changed
	testLogging {
		events "passed", "skipped", "failed", "standardError"
	}
}
